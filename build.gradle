buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'http://repo.quartzpowered.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath 'org.quartzpowered:apigen:1.0-SNAPSHOT'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'apigen'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'org.quartzpowered'
version = '1.0-SNAPSHOT'

defaultTasks 'licenseFormat', 'build'

license {
    include '**/*.java'
    strictCheck true
}

configurations {
    provided
    compile.extendsFrom(provided)
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'http://repo.quartzpowered.org/content/repositories/snapshots' }
}

dependencies {
    provided 'org.projectlombok:lombok:1.16.4'

    compile 'org.quartzpowered:apigen:1.0-SNAPSHOT:api'
    compile 'io.netty:netty-all:5.0.0.Alpha2'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.inject:guice:4.0'
    compile 'com.google.inject.extensions:guice-assistedinject:4.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.esotericsoftware:reflectasm:1.11.0'
    compile 'net.engio:mbassador:1.2.1'
    compile 'io.fastjson:boon:0.33'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.fusesource.jansi:jansi:1.11'
    compile 'org.codehaus.groovy:groovy-all:2.3.10'
    compile 'com.flowpowered:flow-nbt:1.0.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task jarWithDependenciesJar(type: Jar, dependsOn: classes) {
    classifier = 'jar-with-dependencies'
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    with jar

    manifest {
        attributes("Main-Class" : "org.quartzpowered.server.main.Main")
    }
}

artifacts {
    archives sourcesJar, javadocJar, jarWithDependenciesJar
}

publishing {
    repositories {
        maven {
            url 'http://repo.quartzpowered.org/content/repositories/' + (project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases')
            credentials {
                username 'deployment'
                password System.env['maven_pass']
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
            artifact jarWithDependenciesJar
        }
    }
}
